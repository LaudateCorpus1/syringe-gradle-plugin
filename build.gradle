apply plugin: "groovy"
apply plugin: "java-gradle-plugin"
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.avast"
version = "1.0.4"
archivesBaseName = "syringe-gradle-plugin"

targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.avast:syringe_2.10:1.6.13"
    compile "org.reflections:reflections:0.9.9"
    compile "org.apache.commons:commons-vfs2:2.0" // optional but necessary (one of several implementations) dependency of the reflections library
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

//Conditional Signing
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Syringe Gradle Plugin'
                packaging 'jar'
                // artifactid 'syringe-gradle-plugin'
                description 'Supporting Gradle plugin for Syringe library'
                url 'https://github.com/avast/syringe-gradle-plugin'

                scm {
                    connection 'scm:git:git@github.com:avast/syringe-gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:avast/syringe-gradle-plugin.git'
                    url 'https://github.com/avast/syringe-gradle-plugin'
                }

                licenses {
                    license {
                        name 'The BSD 3-Clause License'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id 'jakubjanecek'
                        name 'Jakub Janecek'
                        email 'janecek@avast.com'
                    }
                    developer {
                        id 'noamasor'
                        name 'Noam Asor'
                        email 'asor@avast.com'
                    }
                }
            }
        }
    }
}
